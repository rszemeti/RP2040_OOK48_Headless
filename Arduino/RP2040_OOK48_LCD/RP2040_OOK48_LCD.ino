
// OOK48 Encoder and Decoder LCD version
// Plus Beacon Decoder
// Colin Durbridge G4EML 2025


#include <hardware/dma.h>
#include <hardware/adc.h>
#include "hardware/irq.h"
#include "arduinoFFT.h"
#include <EEPROM.h>
#include <TFT_eSPI.h>                 // Hardware-specific library. Must be pre-configured for this display and touchscreen
#include "DEFINES.h"                  //include the defines for this project
#include "globals.h"                  //global variables
#include "float.h"
#include <SPI.h>
#include "SdFat_Adafruit_Fork.h"
#include "Adafruit_TinyUSB.h"

SdFat sd;

SdFile sdfile;

Adafruit_USBD_MSC usb_msc;                // USB Mass Storage object

#define SD_CONFIG SdSpiConfig(SDCS, SHARED_SPI,SD_SCK_MHZ(12), &SPI1)

TFT_eSPI tft = TFT_eSPI();            // Invoke custom library
 
ArduinoFFT<float> FFT = ArduinoFFT<float>(sample, sampleI, NUMBEROFSAMPLES, SAMPLERATE);         //Declare FFT function

struct repeating_timer TxIntervalTimer;                   //repeating timer for Tx bit interval

struct repeating_timer PPSIntervalTimer;                  // and another for the 1PPS signal delay

//Run once on power up. Core 0 does the time critical work. Core 1 handles the GUI.  
void setup() 
{
    EEPROM.begin(1024);
    loadSettings();
    pinMode(PPSINPUT,INPUT);
    pinMode(KEYPIN,OUTPUT);
    digitalWrite(KEYPIN,0);
    pinMode(TXPIN,OUTPUT);
    digitalWrite(TXPIN,0);
    initGUI();
    app = getApp(); 
    if(app == OOK48)
     {
       mode = RX;  
       RxInit();
       TxMessNo = 0;
       TxInit();
     }
     else           //Beacon Decoder
     {
      if(app == BEACONPI4)
      {
       beaconMode = PI4;
       PI4Init();
      }
      else 
      {
       beaconMode = JT4;
       JT4Init();
      }
     }
      attachInterrupt(PPSINPUT,ppsISR,RISING);
}

//Interrupt called every symbol time to update the Key output. 
bool TxIntervalInterrupt(struct repeating_timer *t)
{
  TxSymbol();
  return true;                    //retrigger this timer
}

//Interrupt called when the 1PPS delay has expired. 
bool PPSIntervalInterrupt(struct repeating_timer *t)
{
  doPPS();
  return false;                   //dont retrigger this timer
}

//1PPS Interrupt 
void ppsISR(void)
{
  if(mode == RX)
  {
   if(settings.rxRetard == 0)           // no delay, call the 1PPS routine immediately
   {
    doPPS();
   }
   else                                 // call the 1PPS routine after a delay 
   {
      add_repeating_timer_ms(settings.rxRetard,PPSIntervalInterrupt,NULL,&PPSIntervalTimer);    //start a delayed callback
   }
  }

  else                //Tx
  {
   if(settings.txAdvance == 0)           // no delay, call the 1PPS routine immediately
   {
    doPPS();
   }
   else                                 // call the 1PPS routine after a delay 
   {
      add_repeating_timer_ms(1000 - settings.txAdvance,PPSIntervalInterrupt,NULL,&PPSIntervalTimer);    //start a delayed callback
   }
  }
}


// Indirect interrupt routine for 1 Pulse per second input Delayed by retard or advance settings. 
void doPPS(void)
{
  PPSActive = 3;              //reset 3 second timeout for PPS signal
  if(app == OOK48)            //don't need to do anything with the PPS when running beacon decoder. 
  {
   if(mode == RX)
    {
      dma_stop();
      dma_handler();        //call dma handler to reset the DMA timing and restart the transfers
      dmaReady = 0;
      if((halfRate == false ) || (halfRate & (gpsSec & 0x01) ))
      {
        cachePoint = 0;        //Reset ready for the first symbol
      }
      else 
      {
        cachePoint = 8;        //Reset ready for the first symbol of the second character
      } 
    } 
   else 
    {
      cancel_repeating_timer(&TxIntervalTimer);                           //Stop the symbol timer if it is running. 
      add_repeating_timer_us(-TXINTERVAL,TxIntervalInterrupt,NULL,&TxIntervalTimer);    // re-start the Symbol timer
      TxSymbol();                       //send the first symbol
    }
  }
}

//core 1 handles the GUI
void setup1()
{
  Serial2.setRX(GPSRXPin);              //Configure the GPIO pins for the GPS module
  Serial2.setTX(GPSTXPin);
  while((settings.baudMagic != 42) || (app == 255))                   //wait for core zero to initialise 
   {
    delay(1);
   }
  Serial2.begin(settings.gpsBaud);    

  SPI1.setRX(SDO);
  SPI1.setTX(SDI);
  SPI1.setSCK(SDCLK);

  sdpresent = sd.begin(SD_CONFIG);

  if(sdpresent)
    {
       SdFile::dateTimeCallback(dateTime);           //set a callback function to set the files attributes. 
    }

  gpsPointer = 0;
  waterRow = 0;
  homeScreen();
}


//Main Loop Core 0. Runs forever. Does most of the work.
void loop() 
{
  if(app == OOK48)
   {
     if(mode == RX)
      {
        RxTick();
      }
     else 
      {
        TxTick();
      }
   }
   else           //Beacon Decoder
   {
     beaconTick();
   }

}


//Core 1 handles the GUI. Including synchronising to GPS if available
void loop1()
{
  uint32_t command;
  char m[64];
  unsigned long inc;
 
    if((gpsSec != lastSec) | (millis() > lastTimeUpdate + 2000))
    {         
      showTime();                                   //display the time
      if(PPSActive >0) PPSActive--;                 //decrement the PPS active timeout. (rest by the next PPS pulse)
      lastSec = gpsSec;
      lastTimeUpdate = millis();
    }


  if(rp2040.fifo.pop_nb(&command))          //have we got something to process from core 0?
    {
      switch(command)
      {
        case GENPLOT:
        generatePlotData();
        break;
        case DRAWSPECTRUM:
        drawSpectrum();
        break;
        case DRAWWATERFALL:
        drawWaterfall();
        break;
        case REDLINE:
        markWaterfall(TFT_RED);
        break;
        case CYANLINE:
        markWaterfall(TFT_CYAN);
        break;
        case MESSAGE:
        textPrintChar(decoded,TFT_BLUE);                                 
        break;
        case TMESSAGE:
        textPrintChar(TxCharSent,TFT_RED);                               
        break;
        case JTMESSAGE:
        sprintf(m,"%02d:%02d %.0lf :%s",gpsHr,gpsMin, sigNoise,JTmessage);
        textPrintLine(m);                                 
        textLine(); 
        break;
        case PIMESSAGE:
        sprintf(m,"%02d:%02d %.0lf :%s",gpsHr,gpsMin, sigNoise,PImessage);
        textPrintLine(m);                                 
        textLine(); 
        break;
        case ERROR:
        textPrintChar(decoded,TFT_ORANGE);                                           
        break;
      }
    }


  if((screenTouched()) && (noTouch))
    {
      processTouch();
    } 

  if(Serial2.available() > 0)           //data received from GPS module
      {
        while(Serial2.available() >0)
          {
            gpsCh=Serial2.read();
            if(gpsCh > 31) gpsBuffer[gpsPointer++] = gpsCh;
            if((gpsCh == 13) || (gpsPointer > 255))
              {
                gpsBuffer[gpsPointer] = 0;
                processNMEA();
                gpsPointer = 0;
              }
          }

      }
}


void processNMEA(void)
{
  float gpsTime;
  float gpsDate;

 gpsActive = true;
 if(RMCValid())                                               //is this a valid RMC sentence?
  {
    int p=strcspn(gpsBuffer , ",") +1;                        // find and skip the first comma
    p= p + strcspn(gpsBuffer+p , ",") + 1;                    // find and skip the second comma 
    if(gpsBuffer[p] == 'A')                                   // is the data valid?
      {
       p=strcspn(gpsBuffer , ",") +1;                         // find and skip the first comma again
       gpsTime = strtof(gpsBuffer+p , NULL);                  //copy the time to a floating point number
       gpsSec = int(gpsTime) % 100;
       gpsTime = gpsTime / 100;
       gpsMin = int(gpsTime) % 100; 
       gpsTime = gpsTime / 100;
       gpsHr = int(gpsTime) % 100;  

       p= p + strcspn(gpsBuffer+p , ",") + 1;                  // find and skip the second comma 
       p= p + strcspn(gpsBuffer+p , ",") + 1 ;                 // find and skip the third comma
       latitude = strtof(gpsBuffer+p , NULL);                  // copy the latitude value
       latitude = convertToDecimalDegrees(latitude);           // convert to ddd.ddd
       p = p + strcspn(gpsBuffer+p , ",") + 1;                 // find and skip the fourth comma  
       if(gpsBuffer[p] == 'S')  latitude = 0-latitude;         // adjust southerly Lats to be negative values                
       p = p + strcspn(gpsBuffer+p , ",") + 1;                 // find and skip the fifth comma      
       longitude = strtof(gpsBuffer+p , NULL);                 // copy the lpngitude value 
       longitude = convertToDecimalDegrees(longitude);         // convert to ddd.ddd   
       p = p + strcspn(gpsBuffer+p , ",") + 1;                 // find and skip the sixth comma  
       if(gpsBuffer[p] == 'W')  longitude = 0 - longitude;     // adjust easterly Longs to be negative values 
       p = p + strcspn(gpsBuffer+p , ",") + 1;                 // find and skip the seventh comma 
       p = p + strcspn(gpsBuffer+p , ",") + 1;                 // find and skip the eighth comma 
       p = p + strcspn(gpsBuffer+p , ",") + 1;                 // find and skip the nineth comma 

       gpsDate = strtof(gpsBuffer+p , NULL);                  //copy the time to a floating point number
       gpsYear = int(gpsDate) % 100;
       gpsDate = gpsDate / 100;
       gpsMonth = int(gpsDate) % 100; 
       gpsDate = gpsDate / 100;
       gpsDay = int(gpsDate) % 100;

       convertToMaid();     
      }
    else
     {
       gpsSec = -1;                                            //GPS time not valid
       gpsMin = -1;
       gpsHr = -1;
       latitude = 0;
       longitude = 0;
       strcpy(qthLocator,"----------");
       qthLocator[settings.locatorLength] = '\0'; // Shorten Locator string
     }
  }


}

bool RMCValid(void)
{
  if((gpsBuffer[3] == 'R') && (gpsBuffer[4] == 'M') && (gpsBuffer[5] == 'C'))
   {
    return checksum(gpsBuffer);
   }
   else 
   {
    return false;
   }
}
// Converts dddmm.mmm format to decimal degrees (ddd.ddd)
float convertToDecimalDegrees(float dddmm_mmm) 
{
    int degrees = (int)(dddmm_mmm / 100);                 // Extract the degrees part
    float minutes = dddmm_mmm - (degrees * 100);         // Extract the minutes part
    float decimalDegrees = degrees + (minutes / 60.0);   // Convert minutes to degrees
    return decimalDegrees;
}

void convertToMaid(void)
{
      // convert longitude to Maidenhead

    float d = 180.0 + longitude;
    d = 0.5 * d;
    int ii = (int)(0.1 * d);
    qthLocator[0] = char(ii + 65);
    float rj = d - 10.0 * (float)ii;
    int j = (int)rj;
    qthLocator[2] = char(j + 48);
    float fpd = rj - (float)j;
    float rk = 24.0 * fpd;
    int k = (int)rk;
    qthLocator[4] = char(k + 65);
    fpd = rk - (float)(k);
    float rl = 10.0 * fpd;
    int l = (int)(rl);
    qthLocator[6] = char(l + 48);
    fpd = rl - (float)(l);
    float rm = 24.0 * fpd;
    int mm = (int)(rm);
    qthLocator[8] = char(mm + 65);
    //  convert latitude to Maidenhead
    d = 90.0 + latitude;
    ii = (int)(0.1 * d);
    qthLocator[1] = char(ii + 65);
    rj = d - 10. * (float)ii;
    j = (int)rj;
    qthLocator[3] = char(j + 48);
    fpd = rj - (float)j;
    rk = 24.0 * fpd;
    k = (int)rk;
    qthLocator[5] = char(k + 65);
    fpd = rk - (float)(k);
    rl = 10.0 * fpd;
    l = int(rl);
    qthLocator[7] = char(l + 48);
    fpd = rl - (float)(l);
    rm = 24.0 * fpd;
    mm = (int)(rm);
    qthLocator[9] = char(mm + 65);
    qthLocator[settings.locatorLength] = '\0'; // Shorten Locator string
}

void loadSettings(void)
{
  bool ss = false;
  EEPROM.get(0,settings);             //read the settings structure

  if(settings.baudMagic != 42)
   {
     if(autoBaud(9600))
      {
        settings.gpsBaud = 9600;
        settings.baudMagic = 42;
        ss=true;
      }
    else 
      {
        settings.gpsBaud = 38400;
        settings.baudMagic = 42;
        ss=true;
      }
   }

   if(settings.messageMagic != 173)
   {
    for(int i=0;i<10;i++)
     {
      strcpy(settings.TxMessage[i] , "EMPTY\r"); 
     } 
    settings.messageMagic = 173;
    ss=true; 
   }

  if((settings.locatorLength <6) || (settings.locatorLength > 10))
   {
    settings.locatorLength = 8;
    ss=true;
   }

  if(settings.decodeMode > 1)
   {
    settings.decodeMode =0;
    ss = true;
   }

  if(settings.txAdvance > 999)
   {
    settings.txAdvance =0;
    ss = true;
   }

  if(settings.rxRetard > 999)
   {
    settings.rxRetard =0;
    ss = true;
   }

  if((settings.batcal < 300) | (settings.batcal > 1000))
   {
    settings.batcal = BATCAL;
   }

   if(ss) saveSettings();
}

void saveSettings(void)
{
  EEPROM.put(0,settings);
  EEPROM.commit();
}

void clearEEPROM(void)
{
  for(int i=0;i<1024;i++)
   {
    EEPROM.write(i,0);
   }
}


bool autoBaud(int rate)
{
  long baudTimer;
  char test[3];
  bool gotit;

  Serial2.begin(rate);                  //start GPS port comms
  baudTimer = millis();                    //make a note of the time
  gotit = false;
  while((millis() < baudTimer+2000) & (gotit == false))       //try 38400 for two seconds
    {
      if(Serial2.available())
       {
         test[0] = test[1];             //shift the previous chars up one
         test[1] = test[2];
         test[2]=Serial2.read();        //get the next char
         if((test[0] == 'R') & (test[1] == 'M') & (test[2] == 'C'))    //have we found the string 'RMC'?
          {
            gotit = true;
          }
       }
    }     
   Serial2.end();     
   return gotit;
}

//replaces a token with an expanded string. returns the result in new. 
void replaceToken(char * news, char * orig, char search, const char * rep)
{
  int outp=0;
  for(int i=0 ; ;i++ )
    {
      if(orig[i] == search)
       {
         for(int q=0 ; ; q++)
          {
            if(rep[q] == 0)
             {
              break;
             }
            news[outp++] = rep[q];
          }
       }
      else 
       {
         news[outp++] = orig[i];
       }
       if(orig[i] == 0)
        {
          break;
        }
    }
}

bool checksum(const char *sentence) 
{
    if (sentence == NULL || sentence[0] != '$') 
    {
        return false;
    }

    const char *checksum_str = strchr(sentence, '*');
    if (checksum_str == NULL || strlen(checksum_str) < 3) 
    {
        return false;
    }

    unsigned char calculated_checksum = 0;
    for (const char *p = sentence + 1; p < checksum_str; ++p) 
    {
        calculated_checksum ^= (unsigned char)(*p);
    }

    unsigned int provided_checksum = 0;
    if (sscanf(checksum_str + 1, "%2x", &provided_checksum) != 1) 
    {
        return false;
    }

    return calculated_checksum == (unsigned char)provided_checksum;
}

//callback, called by FAT library whenever a file is created 
void dateTime(uint16_t* date, uint16_t* time) 
{

  // Set FAT date (bits: YYYYYYYMMMMDDDDD)
  *date = FAT_DATE(gpsYear + 2000, gpsMonth, gpsDay);

  // Set FAT time (bits: HHHHHMMMMMMSSSSS, seconds/2)
  *time = FAT_TIME(gpsHr, gpsMin, gpsSec);
}